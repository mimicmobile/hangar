plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "kotlin-kapt"
}

def keystorePropertiesFile = rootProject.file("key.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace "ca.mimic.hangar"

    defaultConfig {
        applicationId "ca.mimic.hangar"
        minSdk 24
        compileSdkVersion 36
        // Necessary due to major changes >30 that severely limit notification customization
        targetSdk 30
        versionCode 34000
        versionName "4.0.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            if (keystorePropertiesFile.exists()) {
                keyAlias keystoreProperties["keyAlias"]
                keyPassword keystoreProperties["keyPassword"]
                storeFile file(keystoreProperties["storeFile"])
                storePassword keystoreProperties["storePassword"]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17"
    }

    lint {
        abortOnError false
    }
    packaging {
        jniLibs {
            pickFirsts += [
                    "lib/armeabi-v7a/libapp.so",
                    "lib/arm64-v8a/libapp.so",
                    "lib/x86/libapp.so",
                    "lib/x86_64/libapp.so"
            ]
        }
    }
    buildToolsVersion '35.0.0'
}

def flutterDir = "$rootDir/app/settings"
def flutterCmd = System.getenv("FLUTTER_CMD") ?: "fvm flutter"
def javaHome17 = System.getenv("JAVA_17") ?: "/usr/lib/jvm/java-17-openjdk"
def flutterRepo = file("$flutterDir/build/host/outputs/repo")

tasks.register("flutterBuildAarDebug", Exec) {
    workingDir flutterDir
    environment "JAVA_HOME", javaHome17
    commandLine "bash", "-lc", "${flutterCmd} build aar --debug --no-profile --no-release --target-platform=android-arm64"
    inputs.files(file("$flutterDir/pubspec.lock"), fileTree("$flutterDir/lib"), fileTree("$flutterDir/android"))
    outputs.dir(flutterRepo)
}

tasks.register("flutterBuildAarRelease", Exec) {
    workingDir flutterDir
    environment "JAVA_HOME", javaHome17
    commandLine "bash", "-lc", "${flutterCmd} build aar --release --no-debug --no-profile --target-platform=android-arm64"
    inputs.files(file("$flutterDir/pubspec.lock"), fileTree("$flutterDir/lib"), fileTree("$flutterDir/android"))
    outputs.dir(flutterRepo)
}

tasks.configureEach { t ->
    if (t.name == "preDebugBuild") t.dependsOn("flutterBuildAarDebug")
    if (t.name == "preReleaseBuild") t.dependsOn("flutterBuildAarRelease")
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.core:core-ktx:1.17.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'com.google.android.material:material:1.12.0'

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.10.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.10.2'

    implementation "com.squareup.moshi:moshi:1.15.2"
    implementation "com.squareup.moshi:moshi-kotlin:1.15.2"

    testImplementation "junit:junit:4.13.2"
    androidTestImplementation 'androidx.test:runner:1.7.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'

    debugImplementation "com.example.settings:flutter_debug:1.0"
    releaseImplementation "com.example.settings:flutter_release:1.0"
}

